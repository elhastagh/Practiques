# Práctica 5: Buses de comunicación 1(introducción y I2C)
## Introducción
El objetivo de la practica es comprender el funcionamiento de los buses sistemas de comunicación entre periféricos; estos elementos pueden ser internos o externos al procesador.
### Introducción Teórica
La función del bus es permitir la conexión lógica entre los diferentes subsistemas que componen el computador. En su mayoría los buses están formados por conductores metálicos por los cuales se trasmiten señales eléctricas que son enviadas y recibidas con la ayuda de circuitos integrados que manejan un protocolo que les permite trasmitir datos útiles. Además de los datos el bus trasmite otras señales digitales como son las direcciones y señales de control.
Los buses definen su capacidad de acuerdo a la frecuencia máxima de envío y al ancho de los datos. Por lo general estos valores son inversamente proporcionales: si se tiene una alta frecuencia, el ancho de datos debe ser pequeño. Esto se debe a que la interferencia entre las señales (crosstalk) y la dificultad de sincronizarlas, crecen con la frecuencia, de manera que un bus con pocas señales es menos susceptible a esos problemas y puede funcionar a alta velocidad.
Todos los buses de computador tienen funciones especiales como las interrupciones y las DMA que permiten que un dispositivo periférico acceda a una CPU o a la memoria usando el mínimo de recursos.
#### Funcionamiento del bus I2C
Para poder realizar la comunicación con solo un cable de datos, el bus I2C emplea una trama (el formato de
los datos enviados) amplia. La comunicación costa de:
- 7 bits a la dirección del dispositivo esclavo con el que queremos comunicar.
- Un bit restante indica si queremos enviar o recibir información.(R/W)
- Un bit de validación
- Uno o más bytes son los datos enviados o recibidos del esclavo.
- Un bit de validación
Con estos 7 bits de dirección es posible acceder a 112 dispositivos en un mismo bus (16 direcciones de las 128 direcciones posibles se reservan para usos especiales).
Este incremento de los datos enviados (18bits por cada 8bits de datos) supone que, en general, la velocidad del bus I2C es reducida. Además la velocidad estándar de transmisión es de 100khz, con un modo de alta velocidad de 400khz.
```
El estándar I2C define otros modos de funcionamiento, como un envío de
dirección de 8,10 y 12bits, o velocidades de transmisión de 1Mbit/s, 3.
4Mbit/s y 5Mbit/s. No suelen ser empleados en Arduino y bastantes
procesadores como el ESP32.
```
#### Ventajas y desventajas
**Ventajas**
- Requiere pocos cables.
- Dispone de mecanismos para verificar que la señal ha llegado.
**Desventajas**
- Su velocidad es media-baja.
- No es full duplex ( es half duplex ). No se puede enviar y recibir simultáneamente.
- No hay verificación de que el contenido del mensaje es correcto ( solo de la llegada del mensaje).

### Ejercicio 1: Escáner I2C
Para este ejercicio conectaremos distintos dispositivos i2c. Para conectar los dispositivos i2c combiene situar resistencias pullup al inicio del circuito, conectando scl-vcc y sda-vcc, sin embargo existen módulos i2c que vienen con las resistencias pull up integradas, por lo que se le exime al circuito de llevarlas. En nuestro caso utilizaremos los módulos i2c de los que disponemos, dandose la casualidad de tener varios módulos i2c con las resistencias pullup integradas con lo que no será necesario situarlas de forma externa.
Código a implementar para detectar dispositivos i2c y mostrar por puerto serie su dirección:
```
#include  <Arduino.h>
#include  <Wire.h>

void  setup(){
Wire.begin();
Serial.begin(115200);
while (!Serial); // Leonardo: wait for serial monitor
Serial.println("\nI2C Scanner");
}

void  loop(){
byte  error, address;
int  nDevices;
Serial.println("Scanning...");
nDevices  =  0;
for(address  =  1; address  <  127; address++ )
{

// The i2c_scanner uses the return value of
// the Write.endTransmisstion to see if
// a device did acknowledge to the address.
Wire.beginTransmission(address);
error  =  Wire.endTransmission();
if (error  ==  0)
{
Serial.print("I2C device found at address 0x");
if (address<16)
Serial.print("0");
Serial.print(address,HEX);
Serial.println(" !");
nDevices++;
}
else  if (error==4)
{
Serial.print("Unknown error at address 0x");
if (address<16)
Serial.print("0");
Serial.println(address,HEX);
}}
if (nDevices  ==  0)
Serial.println("No I2C devices found\n");

else
Serial.println("done\n");
delay(5000); // wait 5 seconds for next scan
}
```
Con el código anterior conseguimos localizar los dispositivos i2c conectados.
En nuestro caso hemos conectado el VEML7700, un sensor de luz, y un oxímetro, los cuales ambos llevan pull up interno por lo que no nos tenemos que preocupar por poner resistencias externas. En nuestro caso nos ha salido por puerto serie:
```
Scanning...
I2C device found at address 0x10 !
I2C device found at address 0x57 !
done
```
Dónde un dispositivo está en la dirección 0x10 y el otro en 0x57, por lo tanto, lectura i conexión de dispositivos i2c conectados correcta.

### Ejercicio 2
En este ejercicio realizaremos un programa para controlar el sensor MPU-6050 que es un sensor de 6 ejes (3 ejes de acelerómetro y 3 ejes de giroscopio) que se utiliza comúnmente en aplicaciones de monitoreo de movimiento, estabilización y navegación.
Código implementado a continuación:
```
#include  <Wire.h>
#include  <Adafruit_MPU6050.h>
  
Adafruit_MPU6050  mpu;

void  setup() {
Serial.begin(115200);
Wire.begin();

if (!mpu.begin()) {
Serial.println("Error de conexión con el MPU-6050");
while (1);
}

Serial.println("MPU-6050 conectado correctamente.");

}

void  loop() {
sensors_event_t  a, g, temp;
mpu.getEvent(&a, &g, &temp);

Serial.print("Aceleración X: "); Serial.print(a.acceleration.x);
Serial.print(" | Aceleración Y: "); Serial.print(a.acceleration.y);
Serial.print(" | Aceleración Z: "); Serial.println(a.acceleration.z);

Serial.print("Giroscopio X: "); Serial.print(g.gyro.x);
Serial.print(" | Giroscopio Y: "); Serial.print(g.gyro.y);
Serial.print(" | Giroscopio Z: "); Serial.println(g.gyro.z);

delay(500);
}
```
Con este código somos capaces de obtener lecturas por el puerto serie de la aceleración y el giroscopio en los ejes X, Y y Z, cada 500 ms. Anotar el uso de librerías especificas para este dispositivo y funciones propias de ellas.
https://github.com/elhastagh/Practiques/blob/032feb9d8e0979975a3297de25ebfee2f7cdbf27/P5.jpeg

#### Ejercicios subida de nota
Archivo .ini a implementar
```
[env:esp32-s3-devkitm-1]
platform = espressif32
board = esp32-s3-devkitm-1
framework = arduino
monitor_speed = 115200
lib_deps =
  sparkfun/SparkFun MAX3010x Pulse and Proximity Sensor Library@^1.2.0
  adafruit/Adafruit SSD1306@^2.5.7
  adafruit/Adafruit GFX Library@^1.11.9
```
##### Parte 1
Realizar utilizando el display y el sensor anterior un dispositivo que muestre en display la
frecuencia cardíaca
```
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <MAX30105.h>
#include "heartRate.h"

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

MAX30105 particleSensor;

const byte RATE_SIZE = 4; // promedio móvil de 4 valores
byte rates[RATE_SIZE];
byte rateSpot = 0;
long lastBeat = 0;
float beatsPerMinute;
int beatAvg;

void setup() {
  Serial.begin(115200);
  Wire.begin(21, 22); // SDA = 21, SCL = 22

  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    Serial.println(F("SSD1306 no encontrado"));
    while (true);
  }

  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);
  display.setCursor(0, 0);
  display.println("Iniciando sensor...");
  display.display();

  if (!particleSensor.begin(Wire, I2C_SPEED_STANDARD)) {
    Serial.println("Sensor MAX3010x no encontrado");
    display.println("Error sensor!");
    display.display();
    while (true);
  }

  particleSensor.setup(); // configuración por defecto
  particleSensor.setPulseAmplitudeRed(0x0A); // LED rojo encendido
  particleSensor.setPulseAmplitudeIR(0x0A);  // LED IR encendido

  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("Sensor iniciado!");
  display.display();
  delay(1000);
}

void loop() {
  long irValue = particleSensor.getIR();

  if (checkForBeat(irValue)) {
    long delta = millis() - lastBeat;
    lastBeat = millis();
    beatsPerMinute = 60 / (delta / 1000.0);

    if (beatsPerMinute < 255 && beatsPerMinute > 20) {
      rates[rateSpot++] = (byte)beatsPerMinute;
      rateSpot %= RATE_SIZE;

      beatAvg = 0;
      for (byte x = 0; x < RATE_SIZE; x++) beatAvg += rates[x];
      beatAvg /= RATE_SIZE;
    }
  }

  // Mostrar en pantalla
  display.clearDisplay();
  display.setCursor(0, 0);
  display.setTextSize(1);
  display.println("Frecuencia Cardiaca:");
  display.setTextSize(2);
  display.setCursor(0, 16);
  display.print(beatAvg);
  display.println(" BPM");

  display.setTextSize(1);
  display.setCursor(0, 45);
  display.print("SpO2 estimado: ");
  display.print(random(94, 99)); // Valor de oxígeno simulado
  display.println("%");

  display.display();

  delay(500);
}

```

##### Parte 2
Generar una pagina web donde se pueda consultar dicha frecuencia y el contenido de
oxigeno
```
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <MAX30105.h>
#include "heartRate.h"
#include <WiFi.h>
#include <WebServer.h>

// Credenciales WiFi
const char* ssid = "TU_SSID";
const char* password = "TU_PASSWORD";

// WebServer en el puerto 80
WebServer server(80);

// OLED
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

// Sensor
MAX30105 particleSensor;
const byte RATE_SIZE = 4;
byte rates[RATE_SIZE];
byte rateSpot = 0;
long lastBeat = 0;
float beatsPerMinute;
int beatAvg;
int fakeSpO2 = 98;

void handleRoot() {
  String html = "<!DOCTYPE html><html><head><meta charset='utf-8'><meta http-equiv='refresh' content='2'>"
                "<title>Monitor de Salud</title></head><body style='font-family:Arial;text-align:center;'>"
                "<h2>Frecuencia Cardíaca</h2><p style='font-size:2em;'>" + String(beatAvg) + " BPM</p>"
                "<h2>Oxígeno en sangre</h2><p style='font-size:2em;'>" + String(fakeSpO2) + "%</p>"
                "</body></html>";
  server.send(200, "text/html", html);
}

void setup() {
  Serial.begin(115200);
  Wire.begin(21, 22);

  // OLED
  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    Serial.println("Error en pantalla OLED");
    while (true);
  }

  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);
  display.setCursor(0, 0);
  display.println("Iniciando...");
  display.display();

  // Sensor
  if (!particleSensor.begin(Wire, I2C_SPEED_STANDARD)) {
    Serial.println("Sensor MAX3010x no encontrado");
    display.println("Error sensor!");
    display.display();
    while (true);
  }

  particleSensor.setup();
  particleSensor.setPulseAmplitudeRed(0x0A);
  particleSensor.setPulseAmplitudeIR(0x0A);

  // WiFi
  WiFi.begin(ssid, password);
  display.println("Conectando WiFi...");
  display.display();
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("\nConectado a WiFi");
  Serial.print("IP local: ");
  Serial.println(WiFi.localIP());

  // Servidor web
  server.on("/", handleRoot);
  server.begin();

  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("WiFi conectada");
  display.println(WiFi.localIP());
  display.display();
  delay(2000);
}

void loop() {
  long irValue = particleSensor.getIR();

  if (checkForBeat(irValue)) {
    long delta = millis() - lastBeat;
    lastBeat = millis();
    beatsPerMinute = 60 / (delta / 1000.0);

    if (beatsPerMinute < 255 && beatsPerMinute > 20) {
      rates[rateSpot++] = (byte)beatsPerMinute;
      rateSpot %= RATE_SIZE;
      beatAvg = 0;
      for (byte x = 0; x < RATE_SIZE; x++) beatAvg += rates[x];
      beatAvg /= RATE_SIZE;
    }
  }

  // OLED display
  display.clearDisplay();
  display.setCursor(0, 0);
  display.setTextSize(1);
  display.println("Frecuencia Cardiaca:");
  display.setTextSize(2);
  display.setCursor(0, 16);
  display.print(beatAvg);
  display.println(" BPM");

  fakeSpO2 = random(95, 99); // Simula valor SpO2

  display.setTextSize(1);
  display.setCursor(0, 45);
  display.print("SpO2 estimado: ");
  display.print(fakeSpO2);
  display.println("%");

  display.display();

  // Servir web
  server.handleClient();
  delay(500);
}

```
