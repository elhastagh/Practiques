### Práctica 4: Sistemas operativos en tiempo real
El objetivo de la practica es comprender el funcionamiento de un sistema operativo en tiempo real.
Para ello, realizaremos una práctica donde generaremos varias tareas y veremos cómo se ejecutan dividiendo el tiempo de uso de la CPU.
##### Introducción Teórica
A la hora de administrar las tareas que queremos que lleve a cabo nuestro microprocessador nos encontramos con la necesidad de establecer un orden de ejecución ya sea previamente establecido por nosotros o que lo haga internamente a un nivel estructual el procesador. Por ejemplo, teniendo varios casos de uso para querer realizar múltiples tareas en un microcontrolador, nos podriamos encontrar ante un microcontrolador que lee un sensor de temperatura, lo muestra en una pantalla LCD y lo envía a la nube. Puede hacer las tres tareas sincrónicamente, uno tras otro. Pero, ¿qué sucede si está utilizando una pantalla de tinta electrónica que tarda unos segundos en actualizarse?. En ese caso, durante esos segundos de actualización el microcontrolador estará bloqueado, sin poder leer nuevos datos del sensor ni enviarlos a la nube. Esto puede provocar pérdida de datos, lentitud en la respuesta del sistema, o incluso un comportamiento errático en aplicaciones sensibles al tiempo.
Para no encontrarnos ante ese problema, en esta práctica emplearemos FreeRTOS y la gestión de multitareas. Podemos asignar la tarea de actualización de la pantalla a un núcleo del ESP32-S3 y al mismo tiempo ejecutar otras tareas (como la lectura de sensores o la comunicación por WiFi) en el otro núcleo. Así, el sistema sigue siendo responsivo y eficiente, incluso mientras la pantalla termina de actualizarse.
#### Creación Tareas
Para programar una tarea, debe hacer dos cosas: crear una función que contenga el código que desea ejecutar y luego crear una tarea que llame a esta función.
En primer lugar, definimos una función con el código a ejecutar. 
```
void toggleLED(void * parameter){
for(;;){ // infinite loop

// Turn the LED on
digitalWrite(led1, HIGH);

// Pause the task for 500ms
vTaskDelay(500 / portTICK_PERIOD_MS);

// Turn the LED off
digitalWrite(led1, LOW);

// Pause the task again for 500ms
vTaskDelay(500 / portTICK_PERIOD_MS);
}}
```
Aquí nos fijamos en el uso de vTaskDelay() en vez de delay() ya que le dice directamente al programador o planificador que ésta tarea no debe ejecutarse durante un periodo determinado.

En segundo lugar crearemos la tarea dentro del setup().
```
void setup() {
xTaskCreate(
toggleLED, 			// Function that should be called
"Toggle LED", 		// Name of the task (for debugging)
1000, 				// Stack size (bytes)
NULL,				// Parameter to pass
1, 					// Task priority
NULL 				// Task handle
);
}
```
En tercer lugar crearemos tareas que se ejecutarán tan solo una vez, como por ejemplo:
```
void uploadToAWS(void * parameter) {
  // lógica
  vTaskDelete(NULL); // Termina la tarea
}
```
Seguidamente, para hacer como comentabamos al inicio de la practica de hacer que los diferentes nucleos ejecuten procesos cada uno por su cuenta, utilizaremos xTaskCreatePinnedToCore()
```
xTaskCreatePinnedToCore(
  uploadToAWS,
  "Upload to AWS",
  1000,
  NULL,
  1,
  NULL,
  0 // núcleo 0 o 1
);
```
Si quisieramos consultar en qué núcleo se ejecuta una tarea, empleariamos:
```
Serial.println(xPortGetCoreID());
```
Para la eliminación de tareas:
Desde dentro de la tarea: ``` vTaskDelete(NULL);```
Desde otra tarea: Guardamos el identificador con ``` TaskHandle_t``` y lo usamos:
```
TaskHandle_t task1Handle = NULL;
xTaskCreate(task1, "Task 1", 1000, NULL, 1, &task1Handle);
vTaskDelete(task1Handle);
```
Último punto a comentar, existe un orden de prioridad de tareas y con ello un rango de más a menos importancia que también podemos establecer nosotros a cada tarea.
Las prioridades van de 0 a 24 (```0``` - ```configMAX_PRIORITIES```).
El valor más alto es el que representa más prioridad.
Si dos tareas coinciden con el mismo valor de prioridad, se reparten el tiempo de CPU.

## Ejercicio 1
```
#include <Arduino.h>

//Declaración tarea
void anotherTask(void * parameter) {
  for(;;) {
    Serial.println("This is another Task");
    delay(1000);
  }

  // No se alcanza, pero buena práctica
  vTaskDelete(NULL);
}

void setup() {
  Serial.begin(115200);  // Inicialización serial port a 115200 baudios

  // Crear una nueva tarea en paralelo
  xTaskCreate(
    anotherTask,         // Función de la tarea
    "Another Task",      // Nombre de la tarea
    10000,               // Tamaño de pila
    NULL,                // Parámetro
    1,                   // Prioridad
    NULL                 // Handle (no se usa)
  );
}

void loop() {
  Serial.println("This is ESP32-S3 Task");
  delay(1000);
}
```
El código anterior tiene creado como subprograma la "anotherTask", esta tarea es la que haremos que se ejecute en segundo plano. Si nos fijamos, en el setup() inicializamos el serial port y además creamos una tarea, esta tarea llama a la anotherTask que hemos declarado con anterioridad, junto con valores como prioridad, nombre de la tarea, etc. Una vez llegamos al loop() vemos que tan solo ejecuta por el serial port la frase "This is ESP32-S3 Task", sin embargo, en el setup() hemos inicializado y hacemos que se ejecute un programa en segundo plano llamado anotherTask.
Por el puerto serial, podremos ver como aparecen cada segundo, las dos frases, "This is ESP32-S3 Task", correspondiente a la ejecución del programa en primer plano, y "This is another Task", correspondiente a la ejecución del programa en segundo plano.

## Ejercicio 2
En este ejercicio, se nos pide crear un progrma que utilice dos tareas una para encender un led y otra para apagarlo, dichas tareas deberan estar sincronizadas.
En nuestro caso usaremos el led interno del esp32-s3, por lo que requeriremos la librería Adafruit para controlar el led RGB WS2812.
```
#include  <Arduino.h>
#include  <Adafruit_NeoPixel.h>

#define  LED_PIN  48
#define  NUM_LEDS  1

Adafruit_NeoPixel  pixel(NUM_LEDS, LED_PIN, NEO_GRB  +  NEO_KHZ800);

// Semáforo binario
SemaphoreHandle_t  xSemaphore  =  NULL;

// Declaración de tareas
void  turnOnTask(void  *  parameter);
void  turnOffTask(void  *  parameter);  

void  setup() {
Serial.begin(115200);
delay(1000);

pixel.begin();
pixel.setBrightness(50);
pixel.show(); // Apagar el LED inicialmente

// Crear el semáforo binario
xSemaphore  =  xSemaphoreCreateBinary();

if (xSemaphore  ==  NULL) {
Serial.println("Error: no se pudo crear el semáforo");
while (true); // Detener si falla
}

// Dar el semáforo al principio para que la tarea "turnOnTask" pueda comenzar

xSemaphoreGive(xSemaphore);

// Crear las tareas
xTaskCreate(turnOnTask, "Turn On LED", 2048, NULL, 1, NULL);
xTaskCreate(turnOffTask, "Turn Off LED", 2048, NULL, 1, NULL);
}

void  loop() {
// No se usa, FreeRTOS maneja las tareas
}

// Tarea para encender el LED
void  turnOnTask(void  *  parameter) {
for (;;) {
// Tomar el semáforo
if (xSemaphoreTake(xSemaphore, portMAX_DELAY) ==  pdTRUE) {
Serial.println("LED ON");
pixel.setPixelColor(0, pixel.Color(0, 255, 0)); // Verde
pixel.show();
delay(1000); // Mantener encendido 1 segundo

// Liberar el semáforo para que la otra tarea pueda ejecutarse
xSemaphoreGive(xSemaphore);

// Ceder la CPU para permitir que la otra tarea tome el semáforo
taskYIELD(); // Esto fuerza el "cambio de tarea" y da tiempo a la otra tarea
}}}

// Tarea para apagar el LED
void  turnOffTask(void  *  parameter) {
for (;;) {
// Tomar el semáforo
if (xSemaphoreTake(xSemaphore, portMAX_DELAY) ==  pdTRUE) {
Serial.println("LED OFF");
pixel.clear(); // Apagar el LED
pixel.show();
delay(1000); // Mantener apagado 1 segundo

// Liberar el semáforo para que la otra tarea pueda ejecutarse
xSemaphoreGive(xSemaphore);

// Ceder la CPU para permitir que la otra tarea tome el semáforo
taskYIELD(); // Esto fuerza el "cambio de tarea" y da tiempo a la otra tarea
}}}
```
En el codigo anterior empleamos un semáforo para sincronizar las dos tareas turnOnTask y turnOffTask, encargadas de encender y apagar el led, respectivamente. En este ejercicio podemos ver que es prácticamente lo mismo que en el caso del ejercicio 1, sin embargo, en este caso nos hemos encontrado con la problemática de 2 tareas que se ejecutan en segundo plano y que tienen la misma prioridad, ya que su funcionamiento es más bien complementario, y esto nos genera que al querer hacer uso del semáforo, estos dos programas se peleen por él. Esta situación nos ocasionaba la monopolización del semáforo por el primer de los dos programas que hacia uso de él, por lo que el programa complementario no lo llegaba a usar nunca. Para solucionar este problema, como son programas que se complementan, es decir, si uno no se ha ejecutado, el otro no podrá variar el estado del sistema, hemos añadido una linea llamada taskYIELD() al final de cada uno de estas tareas, por lo que una vez ejecutado el programa, usado el semáforo y liberado el semáforo, la propia tarea cede el control de la CPU para que otra tarea que esté lista para ejecutarse lo pueda hacer antes que ella, evitando así que se ejecute la misma tarea en bucle.
Para hacer seguimiento del programa hemos hecho que se encienda y apague el led, y a la vez muestre lo que hace por el serial port.

### Extra: Joc esp32-s3
En este apartado tratamos de hacer un juego interactivo web con el esp32-s3. A continuación el código:
```
/*
* ESP32 RTOS - Juego Simple en Servidor Web con Pulsadores Físicos
*
* Este proyecto implementa un juego de "Atrapa el LED" usando FreeRTOS en un ESP32 con interfaz web.
* Características:
* - Juego interactivo donde se debe presionar un botón cuando el LED correspondiente se enciende
* - Servidor web que muestra puntuación, tiempo restante y controles
* - Pulsadores físicos en la placa ESP32
* - Múltiples tareas concurrentes con RTOS
*/
#include <Arduino.h>
#include <WiFi.h>
#include <AsyncTCP.h>
#include <ESPAsyncWebServer.h>
#include <SPIFFS.h>
#include "freertos/FreeRTOS.h"
#include "freertos/task.h"
#include "freertos/queue.h"
#include "freertos/semphr.h"
// Configuración de red WiFi
const char* ssid = "ESP32_Game"; // Nombre de la red WiFi (punto de acceso)
const char* password = "12345678"; // Contraseña de la red WiFi
// Definición de pines
#define LED1 2
#define LED2 4
#define LED3 5
#define BTN1 16
#define BTN2 17
#define BTN3 18
#define LED_STATUS 19 // LED de estado del juego
// Variables del juego
volatile int puntuacion = 0;
volatile int tiempoJuego = 30; // Duración del juego en segundos
volatile int ledActivo = -1; // LED actualmente activo (-1 = ninguno)
volatile bool juegoActivo = false;
volatile int dificultad = 1; // 1-5, afecta la velocidad del juego
// Variables FreeRTOS
QueueHandle_t botonQueue;
SemaphoreHandle_t juegoMutex;
TaskHandle_t tareaJuegoHandle = NULL;
// Servidor web
AsyncWebServer server(80);
AsyncEventSource events("/events");
// Estructura para los eventos de botones
typedef struct {
uint8_t boton;
uint32_t tiempo;
} EventoBoton;
// Prototipos de tareas
void TareaServidorWeb(void *pvParameters);
void TareaJuego(void *pvParameters);
void TareaLecturaBotones(void *pvParameters);
void TareaTiempo(void *pvParameters);
// Prototipos de funciones
void iniciarJuego();
void detenerJuego();
void activarLED(int led);
void desactivarTodosLEDs();
String obtenerEstadoJuego();
void enviarActualizacionWeb();
// Función para manejar interrupciones de botones
void IRAM_ATTR ISR_Boton(void *arg) {
uint8_t numeroPulsador = (uint32_t)arg;
// Crear un evento para el botón
EventoBoton evento;
evento.boton = numeroPulsador;
evento.tiempo = xTaskGetTickCountFromISR();
// Enviar evento a la cola
xQueueSendFromISR(botonQueue, &evento, NULL);
}
// HTML para la página principal
const char index_html[] PROGMEM = R"rawliteral(
<!DOCTYPE HTML>
<html>
<head>
<title>ESP32 Game - Atrapa el LED</title>
<meta name="viewport" content="width=device-width, initial-scale=1">
<style>
body { font-family: Arial; text-align: center; margin:0px auto; padding: 20px; }
.container { display: flex; flex-direction: column; width: 100%; max-width: 500px; margin: 0 auto; }
.card { background-color: #F8F7F9; box-shadow: 2px 2px 12px 1px rgba(140,140,140,.5); padding: 20px; margin: 10px 0; border-radius: 5px; }
.button { padding: 15px 50px; font-size: 24px; text-align: center; outline: none; color: #fff; background-color: #0f8b8d; border: none; border-radius: 5px; cursor: pointer; }
.button:active { background-color: #0f8b8d; transform: translateY(2px); }
.difficulty { margin: 20px 0; }
.progress-container { width: 100%; background-color: #ddd; border-radius: 5px; margin: 10px 0; }
.progress-bar { height: 30px; background-color: #04AA6D; border-radius: 5px; width: 100%; text-align: center; line-height: 30px; color: white; }
.led-container { display: flex; justify-content: space-around; margin: 20px 0; }
.led { width: 50px; height: 50px; border-radius: 50%; background-color: #333; }
.led.active { background-color: #ff0000; }
#led1 { background-color: #ff0000; }
#led2 { background-color: #00ff00; }
#led3 { background-color: #0000ff; }
.instructions { text-align: left; margin: 20px 0; }
</style>
</head>
<body>
<div class="container">
<h2>ESP32 Game - Atrapa el LED</h2>
<div class="card">
<h3>Panel de Control</h3>
<p>Puntuación: <span id="score">0</span></p>
<p>Tiempo: <span id="time">30</span> segundos</p>
<div class="progress-container">
<div id="progress" class="progress-bar" style="width:100%">30s</div>
</div>
<div class="difficulty">
<label for="difficultyRange">Dificultad: <span id="diffValue">1</span></label>
<input type="range" min="1" max="5" value="1" class="slider" id="difficultyRange">
</div>
<button id="startBtn" class="button">Iniciar Juego</button>
</div>
<div class="card">
<h3>Estado del Juego</h3>
<div class="led-container">
<div class="led" id="led1"></div>
<div class="led" id="led2"></div>
<div class="led" id="led3"></div>
</div>
<p id="gameStatus">Juego detenido</p>
</div>
<div class="card instructions">
<h3>Instrucciones</h3>
<p>1. Presiona el botón físico correspondiente cuando su LED se encienda</p>
<p>2. Ganas 1 punto por cada LED correctamente "atrapado"</p>
<p>3. Pierdes 1 punto si presionas el botón incorrecto</p>
<p>4. Mayor dificultad = LEDs más rápidos = más puntos posibles</p>
</div>
</div>
<script>
var gateway = `ws://${window.location.hostname}/events`;
var websocket;
window.addEventListener('load', onLoad);
function initWebSocket() {
console.log('Trying to open a WebSocket connection...');
websocket = new EventSource(gateway);
websocket.onopen = onOpen;
websocket.onclose = onClose;
websocket.onmessage = onMessage;
}
function onOpen(event) {
console.log('Connection opened');
}
function onClose(event) {
console.log('Connection closed');
setTimeout(initWebSocket, 2000);
}
function onMessage(event) {
var data = JSON.parse(event.data);
document.getElementById('score').innerHTML = data.score;
document.getElementById('time').innerHTML = data.time;
document.getElementById('progress').style.width = (data.time/30*100) + '%';
document.getElementById('progress').innerHTML = data.time + 's';
document.getElementById('gameStatus').innerHTML = data.active ? "Juego en curso" : "Juego detenido";
// Actualizar LEDs
document.getElementById('led1').style.opacity = (data.led === 0) ? "1.0" : "0.3";
document.getElementById('led2').style.opacity = (data.led === 1) ? "1.0" : "0.3";
document.getElementById('led3').style.opacity = (data.led === 2) ? "1.0" : "0.3";
// Actualizar botón
document.getElementById('startBtn').innerHTML = data.active ? "Detener Juego" : "Iniciar Juego";
}
function onLoad(event) {
initWebSocket();
initButton();
}
function initButton() {
document.getElementById('startBtn').addEventListener('click', toggle);
document.getElementById('difficultyRange').addEventListener('change', updateDifficulty);
document.getElementById('difficultyRange').addEventListener('input', function() {
document.getElementById('diffValue').innerHTML = this.value;
});
}
function toggle() {
websocket.close();
var xhr = new XMLHttpRequest();
xhr.open("GET", "/toggle", true);
xhr.send();
setTimeout(initWebSocket, 100);
}
function updateDifficulty() {
var difficulty = document.getElementById('difficultyRange').value;
var xhr = new XMLHttpRequest();
xhr.open("GET", "/difficulty?value=" + difficulty, true);
xhr.send();
}
</script>
</body>
</html>
)rawliteral";
void setup() {
// Inicializar puerto serie
Serial.begin(115200);
Serial.println("Inicializando Juego ESP32 con RTOS y Servidor Web");
// Inicializar SPIFFS
if(!SPIFFS.begin(true)) {
Serial.println("Error al montar SPIFFS");
return;
}
// Configurar pines
pinMode(LED1, OUTPUT);
pinMode(LED2, OUTPUT);
pinMode(LED3, OUTPUT);
pinMode(LED_STATUS, OUTPUT);
pinMode(BTN1, INPUT_PULLUP);
pinMode(BTN2, INPUT_PULLUP);
pinMode(BTN3, INPUT_PULLUP);
// Apagar todos los LEDs al inicio
desactivarTodosLEDs();
digitalWrite(LED_STATUS, LOW);
// Crear recursos RTOS
botonQueue = xQueueCreate(10, sizeof(EventoBoton));
juegoMutex = xSemaphoreCreateMutex();
// Configurar interrupciones para los botones
attachInterruptArg(BTN1, ISR_Boton, (void*)BTN1, FALLING);
attachInterruptArg(BTN2, ISR_Boton, (void*)BTN2, FALLING);
attachInterruptArg(BTN3, ISR_Boton, (void*)BTN3, FALLING);
// Configurar WiFi en modo AP (Access Point)
WiFi.softAP(ssid, password);
IPAddress IP = WiFi.softAPIP();
Serial.print("Dirección IP del AP: ");
Serial.println(IP);
// Rutas del servidor web
server.on("/", HTTP_GET, [](AsyncWebServerRequest *request){
request->send_P(200, "text/html", index_html);
});
server.on("/toggle", HTTP_GET, [](AsyncWebServerRequest *request){
if (juegoActivo) {
detenerJuego();
} else {
iniciarJuego();
}
request->send(200, "text/plain", "OK");
});
server.on("/difficulty", HTTP_GET, [](AsyncWebServerRequest *request){
if (request->hasParam("value")) {
int valor = request->getParam("value")->value().toInt();
if (valor >= 1 && valor <= 5) {
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
dificultad = valor;
xSemaphoreGive(juegoMutex);
}
}
}
request->send(200, "text/plain", "OK");
});
// Configurar servidor de eventos SSE
events.onConnect([](AsyncEventSourceClient *client){
if(client->lastId()){
Serial.printf("Cliente reconectado! Último mensaje recibido: %u\n", client->lastId());
}
client->send(obtenerEstadoJuego().c_str(), NULL, millis(), 10000);
});
server.addHandler(&events);

// Iniciar servidor
server.begin();

// Crear tareas
xTaskCreate(TareaServidorWeb,"WebServerTask",4096,NULL,1,NULL);
xTaskCreate(TareaLecturaBotones,"BotonesTask",2048,NULL,2,NULL);
xTaskCreate(TareaTiempo,"TiempoTask",2048,NULL,1,NULL);
}

void loop() {
// loop() queda vacío en aplicaciones RTOS
vTaskDelay(portMAX_DELAY);
}
// Tarea que maneja actualizaciones periódicas al servidor web
void TareaServidorWeb(void *pvParameters) {
for (;;) {
enviarActualizacionWeb();
vTaskDelay(pdMS_TO_TICKS(200)); // Actualizar aproximadamente 5 veces por segundo
}
}
// Tarea principal del juego
void TareaJuego(void *pvParameters) {
int ultimoLed = -1;
for (;;) {
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
if (juegoActivo) {
// Generar un nuevo LED activo (diferente al anterior)
int nuevoLed;
do {
nuevoLed = random(0, 3); // 0-2 para los tres LEDs
} while (nuevoLed == ultimoLed);
ledActivo = nuevoLed;
ultimoLed = nuevoLed;
// Activar el LED correspondiente
desactivarTodosLEDs();
switch (ledActivo) {
case 0: digitalWrite(LED1, HIGH); break;
case 1: digitalWrite(LED2, HIGH); break;
case 2: digitalWrite(LED3, HIGH); break;
}
}
xSemaphoreGive(juegoMutex);
}
// Esperar un tiempo inversamente proporcional a la dificultad
int espera = 1000 - (dificultad * 150); // 850ms - 250ms según dificultad
vTaskDelay(pdMS_TO_TICKS(espera));
}
}
// Tarea que gestiona los botones y sus acciones
void TareaLecturaBotones(void *pvParameters) {
EventoBoton evento;
uint32_t ultimoTiempoBoton = 0;
const uint32_t debounceTime = pdMS_TO_TICKS(200); // Tiempo anti-rebote
for (;;) {
// Esperar a que llegue un evento desde la interrupción
if (xQueueReceive(botonQueue, &evento, portMAX_DELAY) == pdPASS) {
// Verificar si ha pasado suficiente tiempo desde la última pulsación (anti-rebote)
if ((evento.tiempo - ultimoTiempoBoton) >= debounceTime) {
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
// Procesar el evento solo si el juego está activo
if (juegoActivo) {
int botonPresionado = -1;
// Determinar qué botón se presionó
if (evento.boton == BTN1) botonPresionado = 0;
else if (evento.boton == BTN2) botonPresionado = 1;
else if (evento.boton == BTN3) botonPresionado = 2;
// Verificar si el botón corresponde al LED activo
if (botonPresionado == ledActivo) {
// ¡Correcto! Incrementar puntuación
puntuacion++;
Serial.printf("¡Correcto! Puntuación: %d\n", puntuacion);
} else {
// Incorrecto, decrementar puntuación pero nunca menos de 0
if (puntuacion > 0) puntuacion--;
Serial.printf("Incorrecto. Puntuación: %d\n", puntuacion);
}
}
xSemaphoreGive(juegoMutex);
}
ultimoTiempoBoton = evento.tiempo;
}
}
}
}
// Tarea que controla el tiempo del juego
void TareaTiempo(void *pvParameters) {
for (;;) {
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
if (juegoActivo && tiempoJuego > 0) {
tiempoJuego--;
// Si el tiempo llega a cero, terminamos el juego
if (tiempoJuego == 0) {
Serial.println("¡Tiempo agotado!");
juegoActivo = false;
desactivarTodosLEDs();
}
}
xSemaphoreGive(juegoMutex);
}
vTaskDelay(pdMS_TO_TICKS(1000)); // Actualizar cada segundo
}
}
// Función para iniciar el juego
void iniciarJuego() {
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
// Reiniciar variables del juego
tiempoJuego = 30;
puntuacion = 0;
juegoActivo = true;
digitalWrite(LED_STATUS, HIGH);

// Crear la tarea del juego si no existe
if (tareaJuegoHandle == NULL) {
xTaskCreate(TareaJuego,"JuegoTask",2048,NULL,1,&tareaJuegoHandle);
}

Serial.println("Juego iniciado");
xSemaphoreGive(juegoMutex);
}}

// Función para detener el juego
void detenerJuego() {
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
juegoActivo = false;
ledActivo = -1;
desactivarTodosLEDs();
digitalWrite(LED_STATUS, LOW);
Serial.println("Juego detenido");
xSemaphoreGive(juegoMutex);
}}

// Función para apagar todos los LEDs
void desactivarTodosLEDs() {
digitalWrite(LED1, LOW);
digitalWrite(LED2, LOW);
digitalWrite(LED3, LOW);
}

// Función para obtener el estado actual del juego como JSON
String obtenerEstadoJuego() {
String estado = "{";
if (xSemaphoreTake(juegoMutex, portMAX_DELAY) == pdTRUE) {
estado += "\"score\":" + String(puntuacion);
estado += ",\"time\":" + String(tiempoJuego);
estado += ",\"led\":" + String(ledActivo);
estado += ",\"active\":" + String(juegoActivo ? "true" : "false");
estado += ",\"difficulty\":" + String(dificultad);
xSemaphoreGive(juegoMutex);
}
estado += "}";
return estado;
}
// Función para enviar actualizaciones al cliente web
void enviarActualizacionWeb() {
events.send(obtenerEstadoJuego().c_str(), "update", millis());
}
```
El código anterior consiste en un juego donde el jugador debe presionar el botón correspondiente al led encendido. La estructura es la siguiente:
El servidor web nos muestra un panel de control interactivo con la puntuación del jugador, el tiempo restante del juego, un control para ajustar la dificultad y el estado de los LEDs. Los LEDs son mostrados en la interfaz web y se iluminan según las reglas del juego. El jugador interactúa con el juego mediante tres botones físicos en la placa ESP32, cada uno asociado a un LED. Cuando un LED se enciende, el jugador debe presionar el botón correspondiente. Si lo hace correctamente, obtiene puntos; si presiona el botón incorrecto, pierde puntos. Hay tres LEDs (rojo, verde y azul), y el juego selecciona aleatoriamente cuál LED se debe presionar. El juego permite al jugador ajustar la dificultad mediante un control deslizante en la interfaz web. A medida que la dificultad aumenta, los LEDs se encienden más rápido, lo que incrementa la rapidez con la que el jugador debe reaccionar. El juego tiene una duración de 30 segundos, y el tiempo restante se muestra en la interfaz web en una barra de progreso. Si el tiempo se acaba, el juego se detiene y el estado cambia a "Juego detenido". El estado del juego se actualiza en tiempo real utilizando WebSockets (SSE - Server-Sent Events). El servidor web envía actualizaciones periódicas sobre la puntuación, el tiempo restante, la dificultad, el LED activo y el estado del juego. Nos encontramos con tareas concurrentes organizadas con FreeRTOS, tarea del juego, tarea de botones, tarea de teimpo y tarea del servidor web. Los botones físicos utilizan interrupciones para detectar de manera eficiente las pulsaciones, asegurando que el sistema responda rápidamente sin perder eventos. El estado del juego (si está activo o detenido) se gestiona mediante variables globales y semáforos para evitar accesos simultáneos a los datos compartidos (como la puntuación y el tiempo restante). Tener en cuenta que se utilizan interrupciones para la detección de eventos de los botones físicos.
### Extra: Reloj Digital con Pulsadores y LEDs
En este apartado trataremos de crear un reloj digital con esp32-s3 usando RTOS con las siguientes características:
1. Muestra horas, minutos y segundos
2. Permite ajustar el tiempo mediante pulsadores
3. Utiliza LEDs para indicar estados del reloj
4. Implementa múltiples tareas concurrentes con RTOS
A continuación el código implementado:
```
#include <Arduino.h>
#include "freertos/FreeRTOS.h"
#include "freertos/task.h"
#include "freertos/queue.h"
#include "freertos/semphr.h"
// Definición de pines
#define LED_SEGUNDOS 2 // LED que parpadea cada segundo
#define LED_MODO 4 // LED que indica modo de ajuste
#define BTN_MODO 16 // Botón para cambiar modo (normal/ajuste hora/ajuste minutos)
#define BTN_INCREMENTO 17 // Botón para incrementar valor en modo ajuste
// Variables para el reloj
volatile int horas = 0;
volatile int minutos = 0;
volatile int segundos = 0;
volatile int modo = 0; // 0: modo normal, 1: ajuste horas, 2: ajuste minutos
// Variables FreeRTOS
QueueHandle_t botonQueue;
SemaphoreHandle_t relojMutex;
// Estructura para los eventos de botones
typedef struct {
uint8_t boton;
uint32_t tiempo;
} EventoBoton;
// Prototipos de tareas
void TareaReloj(void *pvParameters);
void TareaLecturaBotones(void *pvParameters);
void TareaActualizacionDisplay(void *pvParameters);
void TareaControlLEDs(void *pvParameters);
// Función para manejar interrupciones de botones
void IRAM_ATTR ISR_Boton(void *arg) {
uint8_t numeroPulsador = (uint32_t)arg;
// Crear un evento para el botón
EventoBoton evento;
evento.boton = numeroPulsador;
evento.tiempo = xTaskGetTickCountFromISR();
// Enviar evento a la cola
xQueueSendFromISR(botonQueue, &evento, NULL);
}
void setup() {
// Inicializar puerto serie
Serial.begin(115200);
Serial.println("Inicializando Reloj Digital con RTOS");
// Configurar pines
pinMode(LED_SEGUNDOS, OUTPUT);
pinMode(LED_MODO, OUTPUT);
pinMode(BTN_MODO, INPUT_PULLUP);
pinMode(BTN_INCREMENTO, INPUT_PULLUP);
// Crear recursos RTOS
botonQueue = xQueueCreate(10, sizeof(EventoBoton));
relojMutex = xSemaphoreCreateMutex();
// Configurar interrupciones para los botones
attachInterruptArg(BTN_MODO, ISR_Boton, (void*)BTN_MODO, FALLING);
attachInterruptArg(BTN_INCREMENTO, ISR_Boton, (void*)BTN_INCREMENTO, FALLING);

// Crear tareas
xTaskCreate(
TareaReloj, // Función que implementa la tarea
"RelojTask", // Nombre de la tarea
2048, // Tamaño de la pila en palabras
NULL, // Parámetros de entrada
1, // Prioridad
NULL // Handle de la tarea
);

xTaskCreate(TareaLecturaBotones,"BotonesTask",2048,NULL,2, // Mayor prioridad para respuesta rápida
NULL);
xTaskCreate(TareaActualizacionDisplay,"DisplayTask",2048,NULL,1,NULL);
xTaskCreate(TareaControlLEDs,"LEDsTask",1024,NULL,1,NULL);

// El planificador RTOS se inicia automáticamente
}
void loop() {
// loop() queda vacío en aplicaciones RTOS
// Todo el trabajo se hace en las tareas
vTaskDelay(portMAX_DELAY);
}

// Tarea que actualiza el tiempo del reloj
void TareaReloj(void *pvParameters) {
TickType_t xLastWakeTime;
const TickType_t xPeriod = pdMS_TO_TICKS(1000); // Periodo de 1 segundo
// Inicializar xLastWakeTime con el tiempo actual
xLastWakeTime = xTaskGetTickCount();
for (;;) {
// Esperar hasta el siguiente periodo
vTaskDelayUntil(&xLastWakeTime, xPeriod);
// Obtener semáforo para acceder a las variables del reloj
if (xSemaphoreTake(relojMutex, portMAX_DELAY) == pdTRUE) {
// Actualizar solo en modo normal
if (modo == 0) {
segundos++;
if (segundos >= 60) {
segundos = 0;
minutos++;
if (minutos >= 60) {
minutos = 0;
horas++;
if (horas >= 24) {
horas = 0;
}}}}
// Liberar el semáforo
xSemaphoreGive(relojMutex);
}}}
// Tarea que gestiona los botones y sus acciones
void TareaLecturaBotones(void *pvParameters) {
EventoBoton evento;
uint32_t ultimoTiempoBoton = 0;
const uint32_t debounceTime = pdMS_TO_TICKS(300); // Tiempo anti-rebote
for (;;) {
// Esperar a que llegue un evento desde la interrupción
if (xQueueReceive(botonQueue, &evento, portMAX_DELAY) == pdPASS) {
// Verificar si ha pasado suficiente tiempo desde la última pulsación (anti-rebote)
if ((evento.tiempo - ultimoTiempoBoton) >= debounceTime) {
// Obtener semáforo para modificar variables del reloj
if (xSemaphoreTake(relojMutex, portMAX_DELAY) == pdTRUE) {
// Procesar el evento según el botón
if (evento.boton == BTN_MODO) {
// Cambiar modo
modo = (modo + 1) % 3;
Serial.printf("Cambio de modo: %d\n", modo);
}
else if (evento.boton == BTN_INCREMENTO) {
// Incrementar valor según el modo actual
if (modo == 1) { // Ajuste de horas
horas = (horas + 1) % 24;
Serial.printf("Horas ajustadas a: %d\n", horas);
}
else if (modo == 2) { // Ajuste de minutos
minutos = (minutos + 1) % 60;
segundos = 0; // Reiniciar segundos al cambiar minutos
Serial.printf("Minutos ajustados a: %d\n", minutos);
}}
// Liberar el semáforo
xSemaphoreGive(relojMutex);
}
// Actualizar tiempo de la última pulsación
ultimoTiempoBoton = evento.tiempo;
}}}}
// Tarea que actualiza la información en el display (puerto serie en este caso)
void TareaActualizacionDisplay(void *pvParameters) {
int horasAnterior = -1;
int minutosAnterior = -1;
int segundosAnterior = -1;
int modoAnterior = -1;
for (;;) {
// Obtener semáforo para leer variables del reloj
if (xSemaphoreTake(relojMutex, portMAX_DELAY) == pdTRUE) {
// Verificar si hay cambios que mostrar
bool cambios = (horas != horasAnterior) ||
(minutos != minutosAnterior) ||
(segundos != segundosAnterior) ||
(modo != modoAnterior);
if (cambios) {
// Imprimir información del reloj
Serial.printf("%02d:%02d:%02d", horas, minutos, segundos);
// Mostrar el modo actual
if (modo == 0) {
Serial.println(" [Modo Normal]");
} else if (modo == 1) {
Serial.println(" [Ajuste Horas]");
} else if (modo == 2) {
Serial.println(" [Ajuste Minutos]");
}
// Actualizar valores anteriores
horasAnterior = horas;
minutosAnterior = minutos;
segundosAnterior = segundos;
modoAnterior = modo;
}
// Liberar el semáforo
xSemaphoreGive(relojMutex);
}
// Esperar antes de la siguiente actualización
vTaskDelay(pdMS_TO_TICKS(100));
}}
// Tarea que controla los LEDs según el estado del reloj
void TareaControlLEDs(void *pvParameters) {
bool estadoLedSegundos = false;
for (;;) {
// Obtener semáforo para leer variables del reloj
if (xSemaphoreTake(relojMutex, portMAX_DELAY) == pdTRUE) {
// LED de segundos parpadea cada segundo
if (segundos != estadoLedSegundos) {
estadoLedSegundos = !estadoLedSegundos;
digitalWrite(LED_SEGUNDOS, estadoLedSegundos);
}
// LED de modo se enciende solo en modo ajuste
digitalWrite(LED_MODO, modo > 0);
// Liberar el semáforo
xSemaphoreGive(relojMutex);
}
// Esperar antes de la siguiente actualización
vTaskDelay(pdMS_TO_TICKS(50));
}}
```
Como hemos podido ver el sistema permite ajustar las horas y minutos mediante botones físicos, y muestra la hora y el modo de ajuste en el puerto serie. Además, se utilizan LEDs para indicar el estado del reloj (por ejemplo, parpadeo de LED por cada segundo y un LED que indica el modo de ajuste). El programa actualiza las variables de tiempo (horas, minutos y segundos) en la tarea `TareaReloj`, la cual se ejecuta cada segundo gracias a un retardo controlado por FreeRTOS. Hay tres modos disponibles: modo normal (para visualizar la hora actual), ajuste de horas y ajuste de minutos. Se cambia entre estos modos mediante el botón `BTN_MODO`, y se incrementan las horas o los minutos mediante el botón `BTN_INCREMENTO`. Un LED parpadea cada segundo para indicar que el reloj está avanzando en el tiempo. Otro LED se enciende solo cuando el sistema está en el modo de ajuste (ya sea de horas o minutos). Se gestionan las interrupciones de los botones para detectar los cambios de modo o incrementar el tiempo en los modos de ajuste. Cada vez que un botón es presionado, un evento se envía a una cola para ser procesado por la tarea `TareaLecturaBotones`. Para garantizar una buena comunicación entre tareas, las tareas usan un semáforo (`relojMutex`) para sincronizar el acceso a las variables compartidas del reloj, evitando que varias tareas modifiquen las mismas variables al mismo tiempo. En el puerto serie se muestra la hora actual, el modo y otros cambios del reloj, permitiendo al usuario seguir el estado del reloj. El programa, permite ajustar el tiempo, visualizar la hora en tiempo real y proporciona retroalimentación mediante LEDs. La sincronización entre las tareas se gestiona correctamente utilizando semáforos, y el sistema es estable con un manejo adecuado de interrupciones y eventos. El comportamiento general es coherente con las expectativas de un reloj digital interactivo.
