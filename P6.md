
# PRACTICA 5 : Buses de comunicación II (SPI)
El objetivo de la practica es comprender el funcionamiento del bus spi.
## Introducción teórica
## BUS SPI
El bus SPI (Serial Peripheral Interface) fue desarrollado por Motorola en 1980. Sus ventajas respecto a otros sistemas han hecho que se convierta en un standard de facto en el mundo de la electrónica y automatización.
El bus SPI tiene una arquitectura de tipo maestro-esclavo. El dispositivo maestro (master) puede iniciar la comunicación con uno o varios dispositivos esclavos (slave), y enviar o recibir datos de ellos. Los dispositivos esclavos no pueden iniciar la comunicación, ni intercambiar datos entre ellos directamente.
En el bus SPI la comunicación de datos entre maestros y esclavo se realiza en dos líneas independientes, una del maestro a los esclavos, y otra de los esclavos al maestro.
Por tanto, la comunicación es Full Duplex, es decir, el maestro puede enviar y recibir datos simultáneamente.
Otra característica de SPI es que es bus síncrono. El dispositivo maestro proporciona una señal de reloj, que mantiene a todos los dispositivos sincronizados. Esto reduce la complejidad del sistema frente a los sistemas asíncronos.
Por tanto, el bus SPI requiere un **mínimo de 3 líneas**.
![](https://www.luisllamas.es/wp-content/uploads/2016/05/arduino-spi-esquema-basico.png)
* MOSI (Master-out, slave-in) para la comunicación del maestro al esclavo.
* MISO (Master-in, slave-out) para comunicación del esclavo al maestro.
* SCK (Clock) señal de reloj enviada por el maestro.

Además, se requiere **una línea adicional SS (Slave Select)** para cada dispositivo esclavo conectado, para seleccionar el dispositivo con el que se va a realizar la comunicación.
![](https://www.luisllamas.es/wp-content/uploads/2016/05/arduino-spi-esquema.png)
Sin embargo, esto tiene la desventaja de requerir una línea por cada dispositivo esclavo.
En caso de disponer muchos dispositivos esclavos esto puede no ser práctico, por lo que es posible adoptar una conexión en cascada, donde cada esclavo trasmite datos al siguiente.
![](https://www.luisllamas.es/wp-content/uploads/2016/05/arduino-spi-esquema-cascada.png)
Por contra, en esta configuración la información debe llegar a todos los esclavos para que la comunicación sea finalizada por lo que, en general, la velocidad de respuesta del bus es menor.
## Funcionamiento del BUS SPI
El funcionamiento del bus SPI es sencillo.
![](https://www.luisllamas.es/wp-content/uploads/2016/05/arduino-spi-funcionamiento.png)
Por defecto el maestro mantiene en estado HIGH todas las líneas SS. Cuando el maestro quiere establecer comunicación con esclavo pone a LOW la línea SS correspondiente, lo que indica al esclavo que debe iniciar la comunicación.
En cada pulso de la señal de reloj, normalmente en el flanco de subida, el dispositivo maestro envía un bit del esclavo y a la vez que recibe un bit del esclavo seleccionado.
La trama (los datos enviados) no sigue ninguna regla, es decir, podemos enviar cualquier secuencia arbitraria de bits. Esto hace que los dispositivos conectados necesiten tener pre-acordado la longitud y significado de los que van a enviar y recibir.
La electrónica requerida para implementar el bus SPI es sencilla y barata, incluso un único registro de desplazamiento puede ser suficiente. Además, como la señal de reloj es proporcionada por el maestro, los esclavos ni siquiera necesitan disponer de un reloj propio.
### Ventajas
- Alta velocidad de trasmisión (hasta 80 Mhz en ESP32) y Full Duplex
- Los dispositivos necesarios son sencillos y baratos, lo que hace que esté integrado en muchos dispositivos.
- Puede mandar secuencias de bit de cualquier tamaño, sin dividir y sin interrupciones.
### Desventajas
- Se requiere 3 cables (SCK, MOSI y MISO) + 1 cable adicional (SS) por cada dispositivo esclavo.
- Solo es adecuado a corta distancias (unos 30cm)s
- No se dispone de ningún mecanismo de control, es decir, no podemos saber si el mensaje ha sido recibido y menos si ha sido recibido correctamente.
- La longitud de los mensajes enviados y recibidos tiene que ser conocida por ambos dispositivos.
## BUS SPI en ESP32
### ESP32
ESP32 tiene 3 buses spi; uno de ellos se utiliza para la carga del programa y no es utilizable, los dos restantes están disponibles para el programador
![](https://i0.wp.com/randomnerdtutorials.com/wp-content/uploads/2018/08/esp32-pinout-chip-ESP-WROOM-32.png?resize=1024%2C523&quality=100&strip=all&ssl=1)
Flash SPI integrado en el ESP-WROOM-32
GPIO 6 a GPIO 11 están expuestos en algunas placas de desarrollo ESP32.
Sin embargo, estos pines están conectados al flash SPI integrado en el chip ESP-WROOM-32 y no se recomiendan para otros usos.
Por lo tanto, **no utilice** estos pines en sus proyectos:
GPIO 6 (SCK/CLK)
GPIO 7 (SDO/SD0)
GPIO 8 (SDI/SD1)
GPIO 9 (SHD/SD2)
GPIO 10 (SWP/SD3)
GPIO 11 (CSC/CMD)
la conexion por defecto es la que sigue :
|SPI |MOSI |MISO| CLK| CS|
|-----|-----|--------|------|-------|
|VSPI| GPIO 23| GPIO 19| GPIO 18| GPIO 5|
|HSPI| GPIO 13| GPIO 12| GPIO 14| GPIO 15|
### ESP32-S3
ESP32-S3 dispone de cuatro buses SPI; dos de ellos se utilizan para conectarse a la memoria Flash y PSRAM.
La conexión por defecto es la que sigue:
|SPI |MOSI |MISO| CLK| CS|
|-----|-----|--------|------|-------|
|SPI2| GPIO 35| GPIO 37| GPIO 36| GPIO 39|
|SPI3| GPIO 11| GPIO 13| GPIO 12| GPIO 10|
Para mas información:
https://www.luisllamas.es/en/esp32-s3-hardware-details-pinout/
## Configuración de SPI para habilitar la comunicación SPI
Si queremos iniciar una comunicación, primero tenemos que habilitar SPI con el siguiente código Arduino
``
SPI.beginTransaction (SPISettings (8000000, MSBFIRST, PI_MODE0));
``
Verá que al comenzar una comunicación SPI hay un total de 3 configuraciones que se pueden configurar, si queremos controlar la configuración manualmente. Esta configuración de SPI no se elimina cuando la comunicación SPI está deshabilitada con ``SPI.endTransaction ()`` .

En su lugar, puede cambiar la configuración de SPI sobrescribiendo la configuración a través de la función``SPISettings ()`` .
Siempre existe la opción de permitir que el microcontrolador establezca la configuración de SPI por defecto.
## Ajuste de frecuencia de reloj para SPI
La primera configuración es la frecuencia de reloj SPI, que se establece en 8 Mbits / s en los ejemplos.(por defecto en ESP32)
## Configuración de cambio de datos para SPI
El segundo ajuste es el desplazamiento de datos que define qué bit se transfiere primero. Hay 2 opciones:
- Bit más significativo (MSB) → MSBFIRST: El bit 8 es el primer bit que se transfiere a través de SPI
- Último bit significativo (LSB) → LSBFIRST: El bit 1 es el primer bit que se transfiere a través de SPI:

|Bit 8 |Bit 7 |Bit 6 |Bit 5 |Bit 4 |Bit 3 |Bit 2 |Bit 1 |
|-------|------|-------|-------|-------|-------|------|-------|
|2⁷ |2⁶ |2⁵ |2⁴ |2³ |2² |2¹ |2⁰ |
|1 |1 |0 |0 |1 |0 |1 |1 |
|MSB | - | - | - |- |- |- | LSB|
La mayoría de los chips SPI utilizan el primer orden de datos MSB.
## Modos de transmisión para SPI
Hay en total 4 modos de transmisión diferentes dependiendo de la combinación de 2 ajustes de transmisión:
* Fase de reloj (CPHA)
* CPHA = 1: Muestras del flanco ascendente del pulso de reloj
* CPHA = 0: Muestras del flanco descendente del pulso de reloj
* Polaridad del reloj (CPOL)
* Reloj inactivo cuando está alto (CPOL = 1): Cada ciclo consta de un pulso de 0. El borde anterior es un borde descendente y el borde posterior es un borde ascendente.
* Reloj inactivo cuando está bajo (CPOL = 0): Cada ciclo consta de un pulso de 1. El borde anterior es un borde ascendente y el borde posterior es un borde descendente.
* 
|Modo |Polaridad del reloj (CPOL) |Fase de reloj (CPHA) |Borde de salida |Captura de datos|
|-------|-----------------------------|-----------------------|----------------------|----------------|
|SPI_MODE0| 0| 0| Descendente| Creciente|
|SPI_MODE1| 0| 1| Creciente| Descendente|
|SPI_MODE2| 1| 0| Creciente| Descendente|
|SPI_MODE3| 1| 1| Descendente| Creciente|
Para la mayoría de los dispositivos, SPI_MODE0 es el modo de transmisión predeterminado.
## EJERCICIOS PRACTICOS
Existen muchos dispositivos que tienen instalado el SPI para su control ; si bien en estas practicas vamos a poner enfasis en aquellos dispositivos que a traves de comentarios de los alumnos son de facil obtencion .
Utilizaremos una lectura de SD
hardware : https://www.amazon.es/gp/product/B06XHJTGGC/ref=ppx_yo_dt_b_asin_title_o08_s00?ie=UTF8&psc=1
![](https://www.electronics-lab.com/wp-content/uploads/2018/02/Micro-SD-Card-Module.jpg)
Utilizaremos una lectura RFID
hardware : https://www.amazon.es/BUYGOO-Keychain-Module-Reader-Arduino/dp/B07D9C82W8/ref=sr_1_7?adgrpid=64168290956&dchild=1&gclid=Cj0KCQjw9_mDBhCGARIsAN3PaFM9-kfIgN51lwJEzMFffK_oXlGh0EMOYz9he8RiqdjbXX8q_oYJGW0aAobkEALw_wcB&hvadid=320777693966&hvdev=c&hvlocphy=1005433&hvnetw=g&hvqmt=e&hvrand=2288607395559069836&hvtargid=kwd-302776364356&hydadcr=11857_1752977&keywords=rc522+rfid+module&qid=1618925460&sr=8-7
![](http://www.naylampmechatronics.com/797-large_default/modulo-rfid-mfrc522-.jpg)
## Ejercicio Practico 1 LECTURA/ESCRITURA DE MEMORIA SD
```
#include <SPI.h>
#include <SD.h>
File myFile;
void setup(){
Serial.begin(115200);
Serial.print("Iniciando SD ...");

if (!SD.begin(4)) {
Serial.println("No se pudo inicializar");
return;
}

Serial.println("inicializacion exitosa");
myFile = SD.open("archivo.txt");//abrimos el archivo
if (myFile) {
Serial.println("archivo.txt:");
while (myFile.available()) {
Serial.write(myFile.read());
}

myFile.close(); //cerramos el archivo
} else {
Serial.println("Error al abrir el archivo");
}}
void loop(){}
```

Qué nos permite éste código ?
Por el puerto serie obtenemos la lectura del archivo.txt qué se halla dentro de la tarjeta SD. En nuestro archivo.txt tenemos el texto: ``Hola buenas``. Así que nuestro programa tratará de abrir un archivo llamado archivo.txt, especificado en una línea del código, donde le decimos qué archivo queremos abrir y mostrar su contenido por el puerto serie. En el caso de hallar el archivo anterior nos mostrará lo siguiente:
```
Iniciando SD ...
inicializacion exitosa
archivo.txt:
Hola buenas
```
En caso de no encontrarlo, y por ello, no poderlo abrir, habrá la siguiente salida por el puerto serie:
```
Iniciando SD ...
inicializacion exitosa
Error al abrir el archivo
```
En caso de no poder inicializar la SD comportaría no poder buscar tampoco el archivo, así pues la salida serie seria la siguiente:
```
Iniciando SD ...
No se pudo inicializar
```
## Ejercicio Practico 2 LECTURA DE ETIQUETA RFID
referencia: https://naylampmechatronics.com/blog/22_tutorial-modulo-lector-rfid-rc522.html
```
#include <SPI.h>
#include <MFRC522.h>

#define RST_PIN 9 //Pin 9 para el reset del RC522
#define SS_PIN 10 //Pin 10 para el SS (SDA) del RC522

MFRC522 mfrc522(SS_PIN, RST_PIN); //Creamos el objeto para el RC522

void setup() {
Serial.begin(9600); //Iniciamos la comunicación serial
SPI.begin(); //Iniciamos el Bus SPI
mfrc522.PCD_Init(); // Iniciamos el MFRC522
Serial.println("Lectura del UID");

}
void loop() {
// Revisamos si hay nuevas tarjetas presentes
if ( mfrc522.PICC_IsNewCardPresent())

{
//Seleccionamos una tarjeta
if ( mfrc522.PICC_ReadCardSerial())
{
// Enviamos serialemente su UID
Serial.print("Card UID:");
for (byte i = 0; i < mfrc522.uid.size; i++) {
Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
Serial.print(mfrc522.uid.uidByte[i], HEX);
}
Serial.println();
// Terminamos la lectura de la tarjeta actual
mfrc522.PICC_HaltA();
}}}
```  
Qué hace posible éste código ?
Al ejecutar éste código obtendremos la siguiente salida:
```
Lectura del UID
Card UID: xx xx xx xx
```
Las `xx` son los **bytes hexadecimales** del **UID** (identificador único) de la tarjeta.

En nuestro caso, tenemos una tarjeta RFID que nos da la siguiente salida por el puerto serie:
```
Lectura del UID
Card UID: 04 A7 5C 9B
```
-   **Librerías necesarias:**
```
#include  <SPI.h>  
#include  <MFRC522.h>
``` 
 -   `SPI.h` maneja la comunicación SPI (necesaria para hablar con el lector RFID).
        
    -   `MFRC522.h` permite controlar el lector RC522.
-   **Definición de pines:**
```
#define RST_PIN 9
#define SS_PIN 10
```
- `RST_PIN` se conecta al pin de **Reset** del módulo RFID (en este caso, pin 9).
-   `SS_PIN` se conecta al pin **SDA** (chip select) del módulo (pin 10).
-   **Creación del objeto RFID:**
    `MFRC522 mfrc522(SS_PIN, RST_PIN);` 
    
-   **`setup()` inicializa:**
    -   Comunicación serie a 9600 baudios (`Serial.begin(9600)`).
    -   Comunicación SPI (`SPI.begin()`).
    -   El lector RFID (`mfrc522.PCD_Init()`).
    -   Muestra el mensaje `"Lectura del UID"`.
-   **En `loop()`:**
    -   Revisa constantemente si **hay una nueva tarjeta cerca**:
        `mfrc522.PICC_IsNewCardPresent()` 
    -   Si hay una tarjeta nueva, intenta **leer su contenido**:
        `mfrc522.PICC_ReadCardSerial()` 
    -   Si la lectura tiene éxito:
        -   Imprime `"Card UID:"` seguido de los bytes que componen el UID de la tarjeta, **en hexadecimal**.
        -   Cada byte del UID se imprime separado por espacios.
    -   Después de leer, **detiene** la comunicación con esa tarjeta usando:
        `mfrc522.PICC_HaltA()`
## Ejercicio Practico 3
A realizar como ejercicio en casa
---------------------------------------------------------------------
***Para la realizacion de este ejercicio pueden utilizar cualquier
elemento spi que tengan disponible*** .
Para este apartado vamos a realizar un proyecto con la pantalla LCD 1.44" TFT SPI.
### Características:
-   **Pantalla LCD 1.44"** con resolución de 128x128 píxeles.
-   Utiliza comunicación SPI. 
-   Generalmente usa un **controlador ST7735**.
-   Perfecta para mostrar información de manera visual (como una interfaz de usuario gráfica).
### Conexión SPI para el TFT:

| **Pin en la pantalla** | **Pin en el ESP32-S3** |
|------------------------|-----------------------------|
| **VCC**                | 3.3V                        |
| **GND**                | GND                         |
| **CS**                 | GPIO 5                      |
| **RESET**              | GPIO 4                      |
| **DC/RS**              | GPIO 2                      |
| **SDI(MOSI)**          | GPIO 23                     |
| **SCK**                | GPIO 18                     |

### Código para ESP32-S3:
```
#include <SPI.h>
#include <TFT_eSPI.h> // Librería para el controlador ST7735

TFT_eSPI tft = TFT_eSPI();  // Crear el objeto para la pantalla

void setup() {
  // Inicia la comunicación con el monitor serial
  Serial.begin(115200);

  // Inicia la pantalla
  tft.init();
  tft.setRotation(3);  // Ajusta la orientación de la pantalla
  tft.fillScreen(TFT_BLACK); // Limpiar la pantalla

  // Configurar el texto
  tft.setTextColor(TFT_WHITE, TFT_BLACK);  // Color de texto blanco sobre negro
  tft.setTextSize(2);  // Tamaño de texto

  // Mostrar mensaje inicial
  tft.setCursor(10, 10);
  tft.println("¡Hola Mundo!");

  // Mostrar algunos gráficos simples
  tft.drawRect(30, 30, 100, 100, TFT_WHITE);  // Rectángulo blanco
  tft.fillCircle(80, 80, 30, TFT_BLUE);  // Círculo azul
}

void loop() {
  // Aquí podrías actualizar la pantalla con más datos
  delay(1000);
}

``` 
----------

### 💬 Explicación del código:
1.  **Librerías**:
    -   `SPI.h`: Librería estándar para comunicación SPI.
    -   `TFT_eSPI.h`: Librería específica para controlar pantallas TFT basadas en el controlador ST7735 (puedes instalarla desde el gestor de librerías de Arduino IDE).
2.  **Inicialización**:
    -   `tft.init()` prepara la pantalla para su uso.
    -   `tft.setRotation(3)` ajusta la orientación de la pantalla.
3.  **Mostrar texto y gráficos**:
    -   `tft.setCursor(10, 10)` coloca el cursor en las coordenadas (10, 10).
    -   `tft.println("¡Hola Mundo!");` imprime el texto en la pantalla.
    -   `tft.drawRect(30, 30, 100, 100, TFT_WHITE);` dibuja un rectángulo blanco.
    -   `tft.fillCircle(80, 80, 30, TFT_BLUE);` dibuja un círculo azul.
## Ejercicio de subida de nota ( muy valorado)

* Parte 1.- Realizar utilizando el sd y el lector rfid escribiendo en un fichero.log la hora y codigo de cada lectura ( describir como se resuelve el hardware para utilizar un spi para dos perifericos) .
### **Código para el ESP32/Arduino**:
Este código realiza las siguientes acciones:
1.  Inicializa el lector RFID y la tarjeta SD.
2.  Cuando se detecta una tarjeta RFID, lee el **UID**.
3.  Guarda en un archivo de registro (`lecturas.log`) en la tarjeta SD la **hora actual** y el **UID** de la tarjeta RFID.
Código:
```
#include <SPI.h>
#include <SD.h>
#include <MFRC522.h>
#include <Wire.h>
#include <RTClib.h>  // Librería para el reloj RTC

#define RST_PIN 22
#define SS_PIN_RFID 5
#define SS_PIN_SD 4

MFRC522 mfrc522(SS_PIN_RFID, RST_PIN);  // Crear el objeto para el lector RFID

RTC_DS3231 rtc;  // Crear objeto para el reloj de tiempo real (RTC)

File logFile;

void setup() {
  Serial.begin(115200);  // Iniciar puerto serie

  // Inicializar el lector RFID
  SPI.begin();  // Iniciar SPI
  mfrc522.PCD_Init();  // Iniciar el lector RFID

  // Inicializar la tarjeta SD
  if (!SD.begin(SS_PIN_SD)) {
    Serial.println("Error al inicializar la tarjeta SD.");
    return;
  }
  Serial.println("Tarjeta SD inicializada.");

  // Inicializar RTC (si no está conectado, puede generar un error)
  if (!rtc.begin()) {
    Serial.println("No se pudo encontrar el RTC.");
    while (1);
  }

  // Verificar si el RTC está funcionando correctamente
  if (rtc.lostPower()) {
    Serial.println("El RTC ha perdido energía. Ajustando la hora.");
    rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));  // Establecer la hora de compilación
  }
}

void loop() {
  // Revisamos si hay una nueva tarjeta RFID
  if (mfrc522.PICC_IsNewCardPresent()) {
    if (mfrc522.PICC_ReadCardSerial()) {
      
      // Obtener la hora actual del RTC
      DateTime now = rtc.now();
      
      // Leer UID de la tarjeta
      String uid = "";
      for (byte i = 0; i < mfrc522.uid.size; i++) {
        uid += String(mfrc522.uid.uidByte[i], HEX);
      }
      uid.toUpperCase();  // Convertir el UID a mayúsculas

      // Imprimir UID en el puerto serie
      Serial.print("UID de la tarjeta: ");
      Serial.println(uid);

      // Crear nombre de archivo log (por ejemplo, "lecturas.log")
      logFile = SD.open("lecturas.log", FILE_WRITE);
      
      // Verificar si el archivo se abrió correctamente
      if (logFile) {
        // Escribir en el archivo el tiempo y UID
        logFile.print("Hora: ");
        logFile.print(now.year(), DEC);
        logFile.print('/');
        logFile.print(now.month(), DEC);
        logFile.print('/');
        logFile.print(now.day(), DEC);
        logFile.print(" ");
        logFile.print(now.hour(), DEC);
        logFile.print(':');
        logFile.print(now.minute(), DEC);
        logFile.print(':');
        logFile.print(now.second(), DEC);
        logFile.print(" - UID: ");
        logFile.println(uid);

        logFile.close();  // Cerrar el archivo
        Serial.println("Registro guardado en el archivo.");
      } else {
        Serial.println("Error al abrir el archivo.");
      }
      
      // Detener la lectura de la tarjeta
      mfrc522.PICC_HaltA();
    }
  }
}

```
### Explicación del código:
1.  **Librerías utilizadas**:
    -   `SPI.h`: Para la comunicación SPI con los periféricos.
    -   `SD.h`: Para la gestión de archivos en la tarjeta SD.
    -   `MFRC522.h`: Para controlar el lector RFID.
    -   `RTClib.h`: Para obtener la hora actual usando un módulo RTC (Real Time Clock).
2.  **Configuración de pines**:
    -   **CS_PIN_RFID** se utiliza para seleccionar el lector RFID.
    -   **CS_PIN_SD** se utiliza para seleccionar la tarjeta SD.
    -   **RST_PIN** es el pin de reinicio del lector RFID.
3.  **Inicialización**:
    -   Se configura el **SPI** para ambos dispositivos, el lector RFID y la tarjeta SD.
    -   El **RTC** se utiliza para obtener la fecha y hora actuales. Si el RTC pierde la energía, se ajusta a la hora del sistema de compilación.
4.  **Lectura RFID**:
    -   El código verifica si se ha detectado una nueva tarjeta RFID.
    -   Cuando se detecta una tarjeta, se lee el **UID** y se obtiene la hora actual.
    -   Se guarda esta información en un archivo de texto (`lecturas.log`) en la tarjeta SD, con la hora y el UID de la tarjeta.
5.  **Archivo en la SD**:
    -   Si el archivo `lecturas.log` ya existe, se añade al final.
    -   Si no existe, se crea un nuevo archivo.

* Parte 2.- Generar una pagina web donde se pueda ver la lectura del lector rfid
A continuación el código que hemos generado para hacer la lectura del código de identificación único de la tarjeta y lo muestra por la página web que hemos especificado su estructura en el mismo código:
```
#include <WiFi.h>
#include <WebServer.h>
#include <SPI.h>
#include <MFRC522.h>

// Configura tus datos WiFi
const char* ssid = "TU_RED_WIFI";
const char* password = "TU_PASSWORD_WIFI";

#define RST_PIN 22
#define SS_PIN 21

MFRC522 mfrc522(SS_PIN, RST_PIN);
WebServer server(80);
String lastUID = "";

void setup() {
  Serial.begin(115200);
  SPI.begin();
  mfrc522.PCD_Init();

  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("Conectado a WiFi. IP: ");
  Serial.println(WiFi.localIP());

  // Ruta para la página web
  server.on("/", HTTP_GET, []() {
    server.send(200, "text/html", paginaHTML());
  });

  // Ruta para obtener el último UID leído
  server.on("/uid", HTTP_GET, []() {
    server.send(200, "text/plain", lastUID);
  });

  server.begin();
}

void loop() {
  server.handleClient();

  if (mfrc522.PICC_IsNewCardPresent() && mfrc522.PICC_ReadCardSerial()) {
    lastUID = "";
    for (byte i = 0; i < mfrc522.uid.size; i++) {
      if (mfrc522.uid.uidByte[i] < 0x10) lastUID += "0";
      lastUID += String(mfrc522.uid.uidByte[i], HEX);
      if (i < mfrc522.uid.size - 1) lastUID += " ";
    }
    lastUID.toUpperCase();
    Serial.println("Nueva tarjeta: " + lastUID);
    mfrc522.PICC_HaltA();
  }
}

// Esta función devuelve el HTML de la página
String paginaHTML() {
  return R"rawliteral(
<!DOCTYPE html>
<html lang="es">
<head>
  <meta charset="UTF-8">
  <title>Lectura RFID</title>
  <style>
    body { text-align: center; font-family: Arial; margin-top: 50px; }
    #uid { font-size: 2em; color: #0078D7; }
  </style>
</head>
<body>
  <h1>Lectura de Tarjeta RFID</h1>
  <p>Última tarjeta detectada:</p>
  <div id="uid">Esperando tarjeta...</div>

  <script>
    setInterval(async () => {
      const res = await fetch('/uid');
      const uid = await res.text();
      if (uid) document.getElementById('uid').innerText = uid;
    }, 1000);
  </script>
</body>
</html>
)rawliteral";
}
```

